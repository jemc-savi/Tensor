:class Tensor.Op.Multiply.Spec
  :is Spec
  :const describes: "Tensor.Op.Multiply"

  :it "computes arithmetic multiplication"
    _WithGraphHelper.run(@env) -> (g, session | assert no_error: (
      result = session.compute!(
        g.multiply!("example"
          g.const!("x", Tensor(I32).from_array([1, 2, 3, 4]))
          g.const!("y", Tensor(I32).from_array([5, 6, 7, 8]))
        )
      )

      assert: result.as!(Tensor(I32)).into_array == [5, 12, 21, 32]
    ))

  :it "can broadcast smaller sizes/shapes across larger sizes/shapes"
    _WithGraphHelper.run(@env) -> (g, session | assert no_error: (
      result = session.compute!(
        g.multiply!("example"
          g.const!("x", Tensor(I32).from_array([1, -3, 5]))
          g.const!("y", Tensor(I32).from_array([
            1, 2, 3
            4, 5, 6
            7, 8, 9
          ]).try_reshape([3, 3]))
        )
      )

      assert: result.as!(Tensor(I32)).into_array == [
        1,  -6, 15
        4, -15, 30
        7, -24, 45
      ]
    ))
